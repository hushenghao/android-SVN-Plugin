apply plugin: 'com.android.application'
// 对应文件名./svn-plugin/src/main/resources/META-INF/gradle-plugins/com.dede.svn-plugin.properties
apply plugin: 'com.dede.svn-plugin'// 如果插件报错先注释当前行，插件编译通过后再打开注释
apply plugin: 'walle'

android {
    compileSdkVersion 27
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.dede.demo"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        flavorDimensions 'svn-demo'
    }

    buildTypes {
        release {
            signingConfig signingConfigs.debug// 调试使用debug签名
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        dev {}
        market {}
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def buildType = variant.buildType.name
            def version = "${variant.versionName}(${variant.versionCode})"
            def flavor = variant.productFlavors[0].name
            outputFileName = "demo_${flavor}_${buildType}_${version}.apk"
        }
    }

}

import com.meituan.android.walle.ChannelReader
import com.meituan.android.walle.ChannelWriter

buildscript {
    repositories {
        maven { url 'http://172.16.0.236:8081/repository/maven-public/' }
        jcenter()
    }
    dependencies {
        classpath "com.meituan.android.walle:payload_writer:$walleVersion"
        classpath "com.meituan.android.walle:payload_reader:$walleVersion"
    }
}

svn_plugin {
    pluginState = true
    svnUrl = "http://svn.guchele.cn/svn/sherry/trunk/app/android/releases/car300"
    msgImport = "上传测试包"
    msgDelete = "删除测试包"
    ignoreDebug = true
    preCommit { file ->
        def channelInfo = ChannelReader.get(file)
        if (channelInfo == null) {
            ChannelWriter.put(file, "haha")
            channelInfo = ChannelReader.get(file)
        }
        println "assembleLast :" + channelInfo.channel
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.meituan.android.walle:library:$walleVersion"
}
